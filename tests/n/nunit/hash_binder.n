# Copyright 2014 the Neutrino authors (see AUTHORS).
# Licensed under the Apache License, Version 2.0 (see LICENSE).

import $assert;
import $core;
import $meta;

def $test_simple() {
  def $s0 := @ctrino.new_hash_stream(4097);
  def $b0 := new @meta:HashBinder($s0);
  # Setting hashes using a single binder.
  def $o0 := @core:manager.new_instance(@core:Object);
  $assert:equals(null, $b0.get_hash_code($o0));
  def $h0 := $b0.get_or_bind_hash_code! $o0;
  $assert:equals($h0, $b0.get_hash_code($o0));
  $assert:equals($h0, $b0.get_or_bind_hash_code! $o0);
  def $o1 := @core:manager.new_instance(@core:Object);
  $assert:equals(null, $b0.get_hash_code($o1));
  def $h1 := $b0.get_or_bind_hash_code! $o1;
  $assert:equals($h1, $b0.get_hash_code($o1));
  $assert:equals($h1, $b0.get_or_bind_hash_code! $o1);
  $assert:not($h0 == $h1);
  def $o2 := @core:manager.new_instance(@core:Object);
  $assert:equals(null, $b0.get_hash_code($o2));
  # Getting those hashes through another binder.
  def $b1 := new @meta:HashBinder($s0);
  $assert:equals($h0, $b1.get_hash_code($o0));
  $assert:equals($h1, $b1.get_hash_code($o1));
  $assert:equals(null, $b1.get_hash_code($o2));
  # Setting a hash through the other binder, getting it through the first one.
  def $h2 := $b1.get_or_bind_hash_code! $o2;
  $assert:equals($h2, $b1.get_hash_code($o2));
  $assert:equals($h2, $b1.get_or_bind_hash_code! $o2);
  $assert:equals($h2, $b0.get_hash_code($o2));
  $assert:equals($h2, $b0.get_or_bind_hash_code! $o2);
  # Freezing the second binder, verifying that it stops receiving updates.
  $core:freeze($b1);
  $assert:equals($h0, $b1.get_hash_code($o0));
  $assert:equals($h1, $b1.get_hash_code($o1));
  $assert:equals($h2, $b1.get_hash_code($o2));
  def $o3 := @core:manager.new_instance(@core:Object);
  def $h3 := $b0.get_or_bind_hash_code! $o3;
  $assert:equals($h3, $b0.get_hash_code($o3));
  $assert:equals($h3, $b0.get_or_bind_hash_code! $o3);
  $assert:equals(null, $b1.get_hash_code($o3));
  # Create a new stream and check that it returns the same values even if the
  # objects are given in a different order.
  def $s1 := @ctrino.new_hash_stream(4097);
  def $b10 := new @meta:HashBinder($s1);
  $assert:equals($h0, $b10.get_or_bind_hash_code! $o3);
  $assert:equals($h1, $b10.get_or_bind_hash_code! $o2);
  $assert:equals($h2, $b10.get_or_bind_hash_code! $o1);
  $assert:equals($h3, $b10.get_or_bind_hash_code! $o0);
  $assert:equals($h0, $b0.get_or_bind_hash_code! $o0);
  $assert:equals($h1, $b0.get_or_bind_hash_code! $o1);
  $assert:equals($h2, $b0.get_or_bind_hash_code! $o2);
  $assert:equals($h3, $b0.get_or_bind_hash_code! $o3);
  ## Create a stream with a different seed and check that it returns all
  ## different values.
  def $s2 := @ctrino.new_hash_stream(4098);
  def $b20 := new @meta:HashBinder($s1);
  $assert:not($h0 == ($b20.get_or_bind_hash_code! $o0));
  $assert:not($h1 == ($b20.get_or_bind_hash_code! $o1));
  $assert:not($h2 == ($b20.get_or_bind_hash_code! $o2));
  $assert:not($h3 == ($b20.get_or_bind_hash_code! $o3));
}

def $test_global() {
  def $b0 := new @meta:HashBinder();
  def $o0 := @core:manager.new_instance(@core:Object);
  $assert:equals(null, $b0.get_hash_code($o0));
  def $h0 := $b0.get_or_bind_hash_code! $o0;
  $assert:equals($h0, $b0.get_hash_code($o0));
  $assert:equals($h0, $b0.get_or_bind_hash_code! $o0);
}

do {
  $test_simple();
  $test_global();
}
