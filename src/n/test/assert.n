# Copyright 2013 the Neutrino authors (see AUTHORS).
# Licensed under the Apache License, Version 2.0 (see LICENSE).

## Primitive assertion library.

import $core;

## Does nothing if the given value is 'true', fails if the value is 'false'.
def $that($value == false) => $fail();
def $that($value == true) => true;

## Does nothing if the given value is 'false', fails if the value is 'true'.
def $not($value == false) => false;
def $not($value == true) => $fail();

## Does nothing if the two given values are collection equal, fails otherwise.
def $equals(expected: $a, found: $b) {
  $that($a ==* $b);
  $b;
}

## Assert that when $a and $b are fulfilled their values are equal. Both $a and
## $b can be either a promise or a concrete value.
def $yields($a, $b) {
  when def $av := $a do {
    when def $bv := $b do {
      $equals($av, $bv);
    }
  }
}

## Aborts the test with a failure.
def $fail() => leave.assert_failed();
